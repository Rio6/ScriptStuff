# Maintainer: Shen Miren <dickeny@gmail.com>
# Contributer: Kan-Ru Chen <kanru@kanru.info>
pkgname=cconv
pkgver=0.6.3
pkgrel=1
pkgdesc="A iconv based simplified-traditional chinese conversion tool"
arch=('i686' 'x86_64')
url="https://github.com/xiaoyjy/cconv"
license=('GPL')
depends=(glibc)
options=(!libtool)
source=("https://github.com/xiaoyjy/cconv/archive/v$pkgver.tar.gz")
sha1sums=('b3f64e03fc30de706201fc51b0f74cd50244157d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/ret > 0/ret >= 0/' main.c
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

